buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTprosjektet/fint-buildscripts/v1.5.0/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://dl.bintray.com/fint/maven'
    }
}

dependencies {
    compile("org.projectlombok:lombok:${lombokVersion}")
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")
    compile('org.glassfish.jersey.core:jersey-client:2.27')
    compile('org.glassfish.jersey.core:jersey-common:2.27')

    compile('no.fint:fint-sse:1.2.1-rc-1')
    compile('no.fint:fint-event-model:3.0.0')
    compile('no.fint:fint-relation-model:1.1.5')
    compile('no.fint:fint-spring-secrets:0.1.0')

    compile('org.springframework.boot:spring-boot-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')

    testCompile("cglib:cglib-nodep:${cglibVersion}")
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
    options.windowTitle = 'FINT Health Adapter'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}



task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.5.0/dependencyReport.gradle'